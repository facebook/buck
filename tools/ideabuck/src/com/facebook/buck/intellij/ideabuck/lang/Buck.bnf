/*
 * Copyright 2015-present Facebook, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License. You may obtain
 * a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations
 * under the License.
 */

{
  parserClass="com.facebook.buck.intellij.ideabuck.lang.BuckParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Buck"
  psiImplClassSuffix="Impl"
  psiPackage="com.facebook.buck.intellij.ideabuck.lang.psi"
  psiImplPackage="com.facebook.buck.intellij.ideabuck.lang.psi.impl"

  elementTypeHolderClass="com.facebook.buck.intellij.ideabuck.lang.psi.BuckTypes"
  elementTypeClass="com.facebook.buck.intellij.ideabuck.lang.psi.BuckElementType"
  tokenTypeClass="com.facebook.buck.intellij.ideabuck.lang.psi.BuckTokenType"

  tokens = [
    NONE = 'None'
    BOOLEAN = 'regexp:(True|False)'

    COMMA = ','
    EQUAL = '='
    DOUBLE_EQUAL = '=='
    NOT_EQUAL = '!='
    GREATER_THAN = '>'
    LESS_THAN = '<'
    GREATER_EQUAL = '>='
    LESS_EQUAL = '<='
    UPDATE_OPS = 'regexp:\+=|-=|\*=|/=|//=|%=|&=|\|=|\^=|<<=|>>='
    BIT_OR = '|'
    BIT_AND = '&'
    BIT_XOR = '^'

    SLASH = '\'
    COLON = ':'
    PLUS = '+'
    MINUS = '-'
    DIVISION = '/'
    DIVISION_INT = '//'
    DOT = '.'
    SEMI_COLON = ';'
    STAR = '*'
    DOUBLE_STAR = '**'

    LINE_COMMENT = 'regexp:#.*'
    DOUBLE_QUOTED_STRING="regexp:\"([^\\\"\r\n]|\\[^\r\n])*\"?"
    SINGLE_QUOTED_STRING="regexp:'([^\\\'\r\n]|\\[^\r\n])*'?"
    DOUBLE_QUOTED_DOC_STRING="regexp:\"\"\".*(\"\"\")?"
    SINGLE_QUOTED_DOC_STRING="regexp:'''.*(''')?"

    LOAD_KEYWORD = 'load'
    DEF = 'def'
    FOR = 'for'
    IN = 'in'
    AND = 'and'
    OR = 'or'
    NOT = 'not'
    IF = 'if'
    ELSE = 'else'
    ELIF = 'elif'
    BREAK = 'break'
    CONTINUE = 'continue'
    PASS = 'pass'
    RETURN = 'return'
    LAMBDA = 'lambda'
    GLOB = 'glob'

    L_PARENTHESES = '('
    L_BRACKET = '['
    L_CURLY = '{'
    R_PARENTHESES = ')'
    R_BRACKET = ']'
    R_CURLY = '}'
    PERCENT = '%'

    NUMBER = 'regexp:-?(0|[1-9]\d*)(\.\d+)?([eE][+-]?\d*)?'
    IDENTIFIER = 'regexp:[a-zA-Z_]([a-zA-Z0-9_])*'

    space = 'regexp:\s+'
  ]
}

buckFile ::= item_*

private item_ ::= (statement | load_call | function_definition)
string ::= (DOUBLE_QUOTED_STRING | SINGLE_QUOTED_STRING | DOUBLE_QUOTED_DOC_STRING | SINGLE_QUOTED_DOC_STRING) (PERCENT primary) ?

load_call ::= 'load' '(' load_target_argument (',' load_argument) (',' load_argument)* [','] ')'
load_target_argument ::= string
load_argument ::= [IDENTIFIER '='] string

function_definition ::= 'def' IDENTIFIER '(' parameter_list? ')' ':' suite

parameter_list ::= parameter (',' parameter)* ','?

parameter ::= IDENTIFIER ['=' single_expression] | '*' IDENTIFIER | '**' IDENTIFIER

suite ::= simple_statement | INDENT statement+ DEDENT

statement ::= simple_statement | if_statement | for_statement | LINE_COMMENT

simple_statement ::= small_statement (';' small_statement)* ';'?

small_statement ::= expression ('=' | UPDATE_OPS) expression | control_flow_statement | expression

control_flow_statement ::= 'return' [expression] | 'break' | 'continue' | 'pass'

expression ::= single_expression (',' single_expression)*

single_expression ::= (primary_with_suffix (binary_op primary_with_suffix)*
                      | 'lambda' [parameter_list] ':' single_expression
                      | 'not' single_expression)
                      ['if' single_expression 'else' single_expression]

primary_with_suffix ::= primary (dot_suffix | slice_suffix)*

primary ::= NONE | BOOLEAN | NUMBER | function_call | IDENTIFIER | string | list | dict | '(' expression ','? ')' | ('+' | '-') primary_with_suffix

function_call ::=  function_name function_call_suffix

function_name ::= ('glob' | IDENTIFIER)

dot_suffix ::= '.' IDENTIFIER [function_call_suffix]

function_call_suffix ::= '(' argument_list? ')'

argument_list ::= argument (',' argument)* ','?

argument ::= [property_lvalue '='] single_expression | '*' single_expression | '**' single_expression

property_lvalue ::= IDENTIFIER

slice_suffix ::= '[' single_expression? ':' single_expression? (':' single_expression?)? ']'

list ::= '[' (list_elements ']' | single_expression comprehension_suffix ']' | ']')

list_elements ::= single_expression (',' single_expression)* ','?

comprehension_suffix ::= 'for' loop_variable 'in' single_expression ['if' single_expression] comprehension_suffix*
loop_variable ::= primary_with_suffix (',' primary_with_suffix)*

dict ::= '{' (dict_entry ((',' dict_entry)* ','? '}' | comprehension_suffix '}') | '}')

dict_entry ::= single_expression ':' single_expression

binary_op ::=  'or' | 'and' | '==' | '!=' | '<' | '>' | '<=' | '>=' | 'in' | 'not' 'in'
                    | '|' | '^' | '&' | '-' | '+' | '*' | '%' | '/' | '//'

if_statement ::= 'if' single_expression ':' suite ('elif' single_expression ':' suite)* ['else' ':' suite]

for_statement ::= 'for' loop_variable 'in' expression ':' suite
