with allow_unsafe_import():
    from sys import platform as os_platform

def java_immutables_library(name, **kwargs):
    deps = kwargs.get('deps', [])
    needed_deps = [
      '//src/com/facebook/buck/util/immutables:immutables',
      '//third-party/java/errorprone:error-prone-annotations',
      '//third-party/java/immutables:immutables',
      '//third-party/java/guava:guava',
      '//third-party/java/jsr:jsr305',
    ]
    for dep in needed_deps:
      if not dep in deps:
        deps.append(dep)

    exported_deps = kwargs.get('exported_deps', [])
    needed_exported_deps = [
      '//third-party/java/immutables:immutables',
    ]
    for dep in needed_exported_deps:
      if not dep in exported_deps:
        exported_deps.append(dep)

    plugins = kwargs.get('plugins', [])
    if not '//third-party/java/immutables:processor' in plugins:
      plugins.append('//third-party/java/immutables:processor')
    keys_to_remove = [
      'plugins',
      'deps',
      'exported_deps',
      'immutable_types',
    ]
    args = {}
    for k in kwargs:
      if not k in keys_to_remove:
        args[k] = kwargs[k]
    return java_library(
      name,
      deps=deps,
      plugins=plugins,
      exported_deps=exported_deps,
      **args)


original_java_test = java_test
def java_test(
    name,
    deps=[],
    vm_args=[],
    labels=[],
    run_test_separately=False,
    **kwargs
    ):
  extra_labels = ['run_as_bundle']
  if run_test_separately:
    extra_labels.append('serialize')

  original_java_test(
    name,
    deps=deps + [
      # When actually running Buck, the launcher script loads the bootstrapper,
      # and the bootstrapper loads the rest of Buck. For unit tests, which don't
      # run Buck, we have to add a direct dependency on the bootstrapper in case
      # they exercise code that uses it.
      '//src/com/facebook/buck/cli/bootstrapper:bootstrapper_lib',
    ],
    vm_args=[
      # Add -XX:-UseSplitVerifier by default to work around:
      # http://arihantwin.blogspot.com/2012/08/getting-error-illegal-local-variable.html
      '-XX:-UseSplitVerifier',

      # Don't use the system-installed JNA; extract it from the local jar.
      '-Djna.nosys=true',

      # Add -Dsun.zip.disableMemoryMapping=true to work around a JDK issue
      # related to modifying JAR/ZIP files that have been loaded into memory:
      #
      # http://bugs.sun.com/view_bug.do?bug_id=7129299
      #
      # This has been observed to cause a problem in integration tests such as
      # CachedTestIntegrationTest where `buck build //:test` is run repeatedly
      # such that a corresponding `test.jar` file is overwritten several times.
      # The CompiledClassFileFinder in JavaTestRule creates a java.util.zip.ZipFile
      # to enumerate the zip entries in order to find the set of .class files
      # in `test.jar`. This interleaving of reads and writes appears to match
      # the conditions to trigger the issue reported on bugs.sun.com.
      #
      # Currently, we do not set this flag in bin/buck_common, as Buck does not
      # normally modify the contents of buck-out after they are loaded into
      # memory. However, we may need to use this flag when running buckd where
      # references to zip files may be long-lived.
      #
      # Finally, note that when you specify this flag,
      # `System.getProperty("sun.zip.disableMemoryMapping")` will return `null`
      # even though you have specified the flag correctly. Apparently sun.misc.VM
      # (http://www.docjar.com/html/api/sun/misc/VM.java.html) saves the property
      # internally, but removes it from the set of system properties that are
      # publicly accessible.
      '-Dsun.zip.disableMemoryMapping=true',
    ] + vm_args,
    run_test_separately=run_test_separately,
    labels = labels + extra_labels,
    **kwargs
  )

def standard_java_library(
    name,
    srcs = None,
    immutable_types = None,
    tests = None,
    resources = None,

    deps = None,
    exported_deps = None,

    provided_deps = None,
    visibility = None,
):
    '''By default, generates a java_library() with the following args:

    java_library(
      name = <DIRECTORY_NAME>,
      srcs = glob(['*.java']),
      deps = deps,
      exported_deps = exported_deps,
      visibility = [
        'PUBLIC',
      ],
    )

    Some of these fields can be overridden.
    '''
    if srcs is None:
        srcs = glob(['*.java'])
    if tests is None:
        tests = []
    if resources is None:
        resources = []
    if provided_deps is None:
        provided_deps = []
    if visibility is None:
        visibility = [ 'PUBLIC' ]

    kwargs = {}
    if immutable_types:
        build_rule = java_immutables_library
    else:
        build_rule = java_library

    build_rule(
        name = name,
        srcs = srcs,
        resources = resources,
        tests = tests,
        deps = deps or [],
        exported_deps = exported_deps or [],
        provided_deps = provided_deps,
        visibility = visibility,
        **kwargs
    )

def standard_java_test(
    name,
    deps = None,
    exported_deps = None,
    provided_deps = None,
    run_test_separately = False,
    vm_args = None,
    fork_mode = 'none',
    labels = None,
):
    if vm_args is None:
        vm_args = ['-Xmx256M']
    srcs = glob(['*.java'])
    test_srcs = []
    for src in srcs:
        if src.endswith('Test.java'):
            test_srcs.append(src)

    if len(test_srcs) > 0:
        java_test(
          name = name,
          srcs = test_srcs,
          resources = glob(['testdata/**'], include_dotfiles=True),
          deps = deps or [],
          exported_deps = exported_deps or [],
          provided_deps = provided_deps or [],
          vm_args = vm_args,
          run_test_separately = run_test_separately,
          fork_mode = fork_mode,
          labels = labels or [],
        )

def standard_java_testutil(name, deps = None, exported_deps = None, provided_deps = None):
    srcs = glob(['*.java'])
    testutil_srcs = []
    for src in srcs:
        if not src.endswith('Test.java'):
            testutil_srcs.append(src)

    if len(testutil_srcs) > 0:
        java_library(
          name = name,
          srcs = testutil_srcs,
          deps = deps or [],
          exported_deps = exported_deps or [],
          provided_deps = provided_deps or [],
          visibility = [
            '//test/...',
          ]
        )
